EventPointerTable(Chapter7Events, PointerList)

setText(0x166, NewChName)

ALIGN 4
NewChName:
String("Ch7: Supply Run")
BYTE 0

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
	OpeningTurnEvent(Turn1TextEvent)
	TurnEventPlayer(0,WagonAIChange,2)
	TurnEventPlayer(0,Wagon2Spawn,5)
	TurnEventPlayer(0,CavCharge,6)
	TurnEventPlayer(0,LoadExtraCavs,7)
	TurnEventPlayer(0,TurnEvent,0,255)
	END_MAIN

ALIGN 32
CharacterBasedEvents:
	CharacterEvent(0,WagonTalkEvent,0,Audinan)
	END_MAIN

MiscBasedEvents:
	AREA 0 ReinforcementZone [0,14] [6,19]
	AREA 0 ReinforcementZone2 [0,13] [7,19]
	AREA 0 BossAiZone [6,0] [12,10]
	AREA 0 Ch7WinCon [6,0] [7,0]
	CauseGameOverIfLordDies
	VeinEffect(4,Ch7VeinEvent)
	END_MAIN

LocationBasedEvents:
	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
	UNIT Curtis Eye Curtis Level(1,Ally,False) [15,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT Sibyl Dancer Curtis Level(8,Ally,False) [16,18] 0x0 0x0 0x0 0x1 [DesertWind, Elixir] NoAI
	UNIT Suhaira Horn Curtis Level(9,Ally,False) [17,18] 0x0 0x0 0x0 0x1 [SteelSword, LanceReaver] NoAI
	UNIT Fabian Wing Curtis Level(9,Ally,False) [17,19] 0x0 0x0 0x0 0x1 [SteelLance, AxeReaver] NoAI
	UNIT Jacob Eye Curtis Level(1,Ally,False) [14,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT Jacob Eye Curtis Level(1,Ally,False) [13,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT Jacob Eye Curtis Level(1,Ally,False) [12,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT Jacob Eye Curtis Level(1,Ally,False) [12,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT Jacob Eye Curtis Level(1,Ally,False) [13,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT Jacob Eye Curtis Level(1,Ally,False) [14,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT Jacob Eye Curtis Level(1,Ally,False) [15,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
	UNIT
	
EnemyUnits:
	UNIT Benjamin Brigand Benjamin Level(12,Enemy,False) [3,1] 0x0 0x0 0x0 0x0 [Halberd] AttackInRangeAI //TwoActionAI
	UNIT BrigandEnemy Outlaw_F Benjamin Level(8,Enemy,True) [7,6] 0x0 0x0 0x0 0x0 [IronBow, Longbow] NeverMoveAI
	UNIT BrigandEnemy Outlaw Benjamin Level(8,Enemy,True) [13,6] 0x0 0x0 0x0 0x0 [IronBow, Longbow] NeverMoveAI
	UNIT BrigandEnemy Brigand Benjamin Level(8,Enemy,True) [4,0] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI //TwoActionAI
	UNIT BrigandEnemy Mercenary Benjamin Level(8,Enemy,True) [3,15] 0x0 0x0 0x0 0x0 [IronBlade] DefaultAI
	UNIT BrigandEnemy Outlaw Benjamin Level(8,Enemy,True) [1,16] 0x0 0x0 0x0 0x0 [IronBow] DefaultAI
	//UNIT BrigandEnemy Brigand Benjamin Level(8,Enemy,True) [0,12] 0x0 0x0 0x0 0x0 [SteelAxe] TwoActionAI
	UNIT BrigandEnemy Brigand Benjamin Level(8,Enemy,True) [4,5] DropItem 0x0 0x0 0x0 [SwordReaver] TwoActionAI
	UNIT BrigandEnemy Mercenary Benjamin Level(8,Enemy,True) [10,2] 0x0 0x0 0x0 0x0 [KillingEdge] AttackInRangeAI
	UNIT BrigandEnemy Brigand Benjamin Level(8,Enemy,True) [16,14] 0x0 0x0 0x0 0x0 [IronAxe] DefaultAI
	UNIT BrigandEnemy Outlaw_F Benjamin Level(8,Enemy,True) [14,12] 0x0 0x0 0x0 0x0 [IronBow] DefaultAI
	UNIT BrigandEnemy Mercenary_F Benjamin Level(8,Enemy,True) [5,9] 0x0 0x0 0x0 0x0 [Zanbato] AttackInRangeAI
	UNIT BrigandEnemy Brigand Benjamin Level(8,Enemy,True) [12,0] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
	
	UNIT Ada GreatLancer Ada Level(1,Enemy,False) [27,2] 0x0 0x0 0x0 0x0 [SilverLance] AttackInRangeAI
	UNIT Farum Tent Ada Level(8,Enemy,True) [29,2] DropItem 0x0 0x0 0x0 [GoddessIcon] NoAI
	UNIT Farum Tent Ada Level(8,Enemy,True) [28,3] DropItem 0x0 0x0 0x0 [IronSword] NoAI
	UNIT Farum SwordKnight Ada Level(8,Enemy,True) [25,19] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
	UNIT Farum BowKnight Ada Level(8, Enemy, True) [29,19] 0x0 0x0 0x0 0x0 [SteelBow] AttackInRangeAI
	UNIT Farum AxeCav Ada Level(9,Enemy,True) [26,4] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI //TwoActionAI
	UNIT Farum LanceCav Ada Level(9,Enemy,True) [27,5] 0x0 0x0 0x0 0x0 [IronLance] AttackInRangeAI //TwoActionAI
	UNIT Farum Wagon Lagrange Level(10,Enemy,True) [27,19] DropItem 0x0 0x0 0x0 [KnightCrest] DoNothing
	UNIT Farum Knight Ada Level(8,Enemy,True) [21,14] 0x0 0x0 0x0 0x0 [ShortSpear] NeverMoveAI
	UNIT Farum Fighter Ada Level(8,Enemy,True) [29,11] 0x0 0x0 0x0 0x0 [IronAxe] TwoActionAI
	UNIT Farum Soldier Ada Level(8,Enemy,True) [30,12] 0x0 0x0 0x0 0x0 [IronLance] TwoActionAI
	UNIT Farum AxeKnight Ada Level(8,Enemy,True) [25,2] 0x0 0x0 0x0 0x0 [SilverAxe] AttackInRangeAI
	UNIT
	
WagonUnit:
	UNIT Audinan Wagon Curtis Level(10,NPC,True) [16,19] 0x0 0x0 0x0 0x0 [0x0] Ch7GrnWagonAI
	UNIT

EnemyWagon2:
	UNIT Farum Wagon Lagrange Level(10,Enemy,True) [27,19] DropItem 0x0 0x1 REDA27R19 [RedGem] SeizerAI 
	UNIT Farum Soldier Ada Level(9,Enemy,True) [26,19] 0x0 0x0 0x1 REDA26R19 [ShortSpear] Ch7WagonEscortAI
	UNIT Farum Mercenary_F Ada Level(9,Enemy,True) [28,19] 0x0 0x0 0x1 REDA28R19 [DawnEdge] Ch7WagonEscortAI
	UNIT

ZoneBrigands:
	UNIT BrigandEnemy Myrmidon_F Benjamin Level(8,Enemy,True) [3,6] 0x0 0x0 0x1 REDA3R8 [SteelSword] DefaultAI
	UNIT BrigandEnemy Outlaw Benjamin Level(8,Enemy,True) [3,6] 0x0 0x0 0x1 REDA2R7 [SteelBow] DefaultAI
	UNIT BrigandEnemy Brigand Benjamin Level(8,Enemy,True) [3,6] 0x0 0x0 0x1 REDA4R7 [SteelAxe] DefaultAI
	UNIT
	
ExtraCavs:
	UNIT Farum SwordCav Ada Level(8,Enemy,True) [26,19] 0x0 0x0 0x1 REDA26R19 [IronSword] DefaultAI
	UNIT Farum LanceCav Ada Level(8,Enemy,True) [28,19] 0x0 0x0 0x1 REDA28R19 [IronLance] DefaultAI
	UNIT

BigLad:
	UNIT Slayers Berserker Ada Level(5,Enemy,True) [27,0] 0x0 0x0 0x1 REDA27R0 [SlayingAxe] DefaultAI
	UNIT
	
	
Traps1:
	MapSpriteTrap(IndicatorTrapID,6,0)
	MapSpriteTrap(IndicatorTrapID,7,0)
	MapSpriteTrap(RedIndicatorTrapID,26,0)
	MapSpriteTrap(RedIndicatorTrapID,27,0)
	Vein(20,5,4,Ch7DVBoxText)
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
	SetBackground(Outside_Village_Day)
	Text(Ch7OpeningText)

	LOAD1 0x0 Units1
	ENUN

	LOAD1 0x1 WagonUnit
	ENUN
	
	LOAD1 0x1 EnemyUnits
	ENUN
	GotoPrepScreen
	ENDA

Turn1TextEvent:

	MUSCFAST EarthboundID
	CAMERA [30,19]
	CURSOR_FLASHING [27,19]
	STAL 80
	REMOVE_CURSORS
	STAL 10
	CAMERA  [9,19]
	Text(Ch7Turn1Text)
	NoFade
	ENDA
	
WagonAIChange:
	SVAL 1 0x00040C00
	CHAI [27,19]
	NoFade
	ENDA

Wagon2Spawn:
	EVBIT_T 9
	LOAD1 0x1 EnemyWagon2
	ENUN
	NoFade
	ENDA

ReinforcementZone:
	ContinueIfFactionMatch(0x0) //Player
	CHECK_EVENTID 8
	BNE 1 s0 sC
	ENUT 8
	EVBIT_T 9
	LOAD1 0x1 ZoneBrigands
	ENUN
	
	LABEL 1
	NoFade
	ENDA

ReinforcementZone2:
	ContinueIfFactionMatch(0x1) //NPC
	CHECK_EVENTID 8
	BNE 3 s0 sC
	ENUT 8
	EVBIT_T 9
	LOAD1 0x1 ZoneBrigands
	ENUN
	
	LABEL 3
	NoFade
	ENDA
	
BossAiZone:
	ContinueIfFactionMatch(0x1)
	CHECK_EVENTID 9
	BNE 2 s0 sC
	
	ENUT 9
	CAMERA Benjamin
	Text(Ch7BossRushText)	
	SETVAL 1 0x00040000
	CHAI Benjamin
	CHAI BrigandEnemy
	SETVAL 1 0x00000303
	CHAI [7,6]
	
	LABEL 2
	NoFade
	ENDA
	
CavCharge:
	SETVAL 1 0x00040000
	CHAI [26,4]
	CHAI [27,5]
	NoFade
	ENDA
	
LoadExtraCavs:
	EVBIT_T 9
	LOAD1 0x1 ExtraCavs
	ENUN
	NoFade
	ENDA

Ch7VeinEvent:
	VeinActivation
	SVAL 1 0xE
	ASMC ASMC_StatusAll|1
	NoFade
	ENDA

WagonTalkEvent:

	CHECK_EVENTID 12
	BNE 4 s0 sC
	SVAL 1 0x20040306 //true do nothing
	CHAI Audinan
	ENUT 12
	CenterTutorialTextBox
	TUTORIALTEXTBOXSTART
	TEXTSHOW Ch7WagonStop
	TEXTEND
	REMA
	GOTO 5

	LABEL 4
	SVAL 1 0x00041106 //grn wagon ai
	CHAI Audinan
	ENUF 12
	CenterTutorialTextBox
	TUTORIALTEXTBOXSTART
	TEXTSHOW Ch7WagonGo
	TEXTEND
	REMA

	LABEL 5
	NoFade
	ENDA
	
Ch7WinCon:
	ContinueIfFactionMatch(0x1)
	CALL EndingScene
	ENDA

TurnEvent:
	CHECK_EVENTID 12
	BEQ 6 sC s0
	
	COUNTER_INC 0
	COUNTER_CHECK 0
	SVAL 1 4
	BNE 8 s1 sC
	
	EVBIT_T 9
	LOAD1 0x1 BigLad
	ENUN
	COUNTER_SET 0 0
	GOTO 7
	
	LABEL 8
	SVAL 1 3
	BNE 7 s1 sC
	CAMERA Audinan
	Text(Ch7WarningText)
	GOTO 7
	
	LABEL 6
	COUNTER_DEC 0
	
	LABEL 7
	NoFade
	ENDA

	
EndingScene:
	MNC2 Chapter8
	NoFade
	ENDA