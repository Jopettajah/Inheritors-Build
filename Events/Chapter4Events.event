EventPointerTable(Chapter4Events, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:

	TurnEventPlayer(0,CounterCheck,0,13)
	TurnEventPlayer(0,Turn3Reinforcements,3)
	TurnEventPlayer(0,Turn5Reinforcements,5)
	TurnEventEnemy(9,BridgeEvent,7)
	TurnEventPlayer(10,BridgeCavsSpawn,8)
	TurnEventPlayer(11,AIChanges,10)
	//TurnEventPlayer(0,EndingScene,13)
	Survive(EndingScene,13)
	END_MAIN

ALIGN 32
CharacterBasedEvents:
	CharacterEvent(8,GarryRecruitment,Curtis,Garry)
	END_MAIN

MiscBasedEvents:
	CauseGameOverIfLordDies
	VeinEffect(7,VeinEvent)
	END_MAIN

LocationBasedEvents:
	Armory(Ch4Armory,4,9)
	Vendor(Ch4Shop,20,2)
	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
	UNIT Curtis Eye Curtis Level(1,Ally,False) [9,14] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [7,14] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [8,14] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [10,14] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [7,15] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [8,15] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [9,15] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [10,15] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [11,14] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [11,15] 0x00 0x00 0x00 0x01 [0x0] NoAI
	//UNIT Janessa CerapterKnight Curtis Level(1,Ally,False) [1,15] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT

EnemyUnits:

	UNIT Bazba Paladin 0x0 Level(1,Enemy,False) [25,6] 0x0 0x0 0x0 0x0 [SteelSword, HandAxe] NeverMoveAI
	UNIT Farum Ballistician Bazba Level(1,Enemy,True) [24,6] 0x0 0x0 0x0 0x0 [Ballista] NeverMoveAI
	UNIT Engineer EngiClass Bazba Level(3,Enemy,True) [25,3] 0x0 0x0 0x0 0x0  [Hammer] DoNothing
	UNIT Engineer EngiClass Bazba Level(3,Enemy,True) [25,2] 0x0 0x0 0x0 0x0  [Hammer] DoNothing
	UNIT Farum PegasusKnight Bazba Level(5,Enemy,True) [20,15] 0x0 0x0 0x0 0x0 [IronLance] DefaultAI
	UNIT Farum SwordCavalier Bazba Level(6,Enemy,True) [25,10] 0x0 0x0 0x0 0x0 [SteelSword Armorslayer] DefaultAI
	UNIT Farum AxeCavalier Bazba Level(5,Enemy,True) [26,9] 0x0 0x0 0x0 0x0 [HandAxe] DefaultAI
	UNIT Farum LanceCavalier Bazba Level(5,Enemy,True) [26,11] 0x0 0x0 0x0 0x0 [Javelin] DefaultAI	
	UNIT Farum AxeKnight Bazba Level(6,Enemy,True) [24,9] 0x0 0x0 0x0 0x0 [SteelAxe,HandAxe] AttackInRangeAI
	UNIT Farum AxeKnight Bazba Level(6,Enemy,True) [24,11] 0x0 0x0 0x0 0x0 [SteelAxe,HandAxe] AttackInRangeAI
	UNIT

EnemyUnits2:
	UNIT Farum LanceKnight Bazba Level(5,Enemy,True) [4,10] 0x0 0x0 0x0 0x0 [SteelLance, Javelin] NeverMoveAI
	UNIT Farum BowKnight Bazba Level(5,Enemy,True) [0,12] 0x0 0x0 0x0 0x0 [SteelBow] AttackInRangeAI
	UNIT Farum AxeKnight Bazba Level(5,Enemy,True) [1,11] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
	UNIT Farum SwordKnight Bazba Level(5,Enemy,True) [1,13] 0x0 0x0 0x0 0x0 [IronBlade] AttackInRangeAI
	UNIT Farum Soldier Bazba Level(5,Enemy,True) [17,7] 0x0 0x0 0x0 0x0 [SteelLance,Javelin] DefaultAI
	UNIT Farum Mercenary Bazba Level(5,Enemy,True) [6,7] 0x0 0x0 0x0 0x0 [SteelSword] DefaultAI
	UNIT Farum Myrmidon_F Bazba Level(5,Enemy,True) [18,13] 0x0 0x0 0x0 0x0 [SteelSword] AttackInRangeAI
	UNIT Farum Fighter Bazba Level(5,Enemy,True) [19,12] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
	UNIT
	
Turn3Cavs:
	UNIT Farum AxeCavalier Bazba Level(5,Enemy,True) [26,9] 0x0 0x0 0x0 0x0 [IronAxe, HandAxe] DefaultAI
	UNIT Farum PegasusKnight Bazba Level(5,Enemy,True) [26,10] 0x0 0x0 0x0 0x0 [IronLance] DefaultAI	//TODO: change to seize ai
	UNIT Farum LanceCavalier Bazba Level(5,Enemy,True) [26,11] 0x0 0x0 0x0 0x0 [SteelLance] DefaultAI
	UNIT

KillerSquad:
	UNIT Farum AxeCavalier Bazba Level(4,Enemy,True) [26,9] 0x0 0x0 0x0 0x0 [KillerAxe] DefaultAI
	UNIT Farum Soldier Bazba Level(4,Enemy,True) [26,10] 0x0 0x0 0x1 REDA25R10 [KillerLance] DefaultAI
	UNIT Farum SwordCavalier Bazba Level(4,Enemy,True) [26,11] 0x0 0x0 0x0 0x0 [KillingEdge] DefaultAI
	UNIT
	
BridgeCavs:
	UNIT Farum SwordCavalier Bazba Level(5,Enemy,True) [26,3] 0x0 0x0 0x0 0x0 [SteelSword] DefaultAI //TODO: change to seize ai
	UNIT Farum LanceCavalier Bazba Level(5,Enemy,True) [26,2] 0x0 0x0 0x0 0x0 [SteelLance] DefaultAI //TODO: change to seize ai
	UNIT
	
HollieGroup:
	UNIT Hollie BowKnight Garry Level(4,NPC,False) [16,7] 0x0 0x0 0x0 [SteelBow] NeverMoveAI
	UNIT
	
GreenGroup:
	UNIT Garry AxeKnight 0x0 Level(5,NPC,False) [12,5] 0x0 0x0 0x1 REDA12R7 [SteelAxe] NeverMoveAI
	UNIT Pieter Soldier Garry Level(3,NPC,False) [11,5] 0x0 0x0 0x1 REDA11R6 [IronLance] NeverMoveAI
	UNIT

Traps1:
	Vein(10,9,7)
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:

	ENUT 10 //Flag for bridge cavs
	FADU 10

	MUSC OffToWarID
	LOAD1 0x1 HollieGroup
	ENUN

	LOAD1 0x1 EnemyUnits
	ENUN
	
	EVBIT_T 9
	MOVE 0 Hollie [13,6]
	ENUN
	TILECHANGE 2
	LOAD1 0x1 GreenGroup
	ENUN
	
	SOUN SwushSFX
	STAL 60
	SOUN CrashSFX
	TILECHANGE 0
	
	//MOVE 0 Garry [12,7]
	
	FADI 10
	
	LOAD1 0x1 EnemyUnits2
	ENUN
	
	LOAD1 0x0 Units1
	ENUN
	GotoPrepScreen
	ENDA

GarryRecruitment:
	MUSS FlapperGirlID
	CUSA Garry
	CUSA Hollie
	CUSA Pieter
	ENUN
	MURE 5
	NoFade
	ENDA
	
VeinEvent:
	VeinActivation
	TILECHANGE 3
	NoFade
	ENDA

Turn3Reinforcements:
	
	EVBIT_T 9
	LOAD1 0x1 Turn3Cavs
	ENUN
	NoFade
	ENDA

Turn5Reinforcements:
	
	EVBIT_T 9
	LOAD1 0x1 KillerSquad
	ENUN
	NoFade
	ENDA
	
BridgeEvent:

	CHECK_EXISTS Engineer
	CAMERA Engineer
	BEQ 1 s0 sC
	TILECHANGE 1
	REMOVEUNIT Engineer
	ENUN
	REMOVEUNIT Engineer
	ENUN
	ENUF 10
	//LOAD1 0x1 BridgeCavs
	//ENUN
	
	LABEL 1
	NoFade
	ENDA

BridgeCavsSpawn:
	EVBIT_T 9
	LOAD1 0x1 BridgeCavs
	ENUN
	NoFade
	ENDA

CounterCheck:
	CHECK_EVENTID 7
	BNE 1 s0 sC	
	
	COUNTER_CHECK 0
	SVAL 1 3
	BNE 1 s1 sC
	
	TILEREVERT 3
	
	LABEL 1
	COUNTER_INC 0
	NoFade
	ENDA

AIChanges:
	
	//Text()
	SETVAL 1 0x00400000
	CHAI	Farum
	NoFade
	ENDA
	
ALIGN 4
Ch4Armory:
SHORT IronSword
SHORT SteelSword
SHORT IronLance
SHORT SteelLance
SHORT IronAxe
SHORT SteelAxe
SHORT Hammer
SHORT IronBow
SHORT SteelBow
SHORT Longbow
SHORT 0
SHORT 0	

ALIGN 4
Ch4Shop:
SHORT Fire
SHORT Elfire
SHORT Heal
SHORT Vulnerary
SHORT 0
SHORT 0
	
EndingScene:
	MNC2 Chapter5
	NoFade
	ENDA