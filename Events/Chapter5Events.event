EventPointerTable(Chapter5Events, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
	TurnEventPlayer(0,Turn2Escapee,2)
	TurnEventPlayer(0,Turn3Escapee,3)
	END_MAIN

ALIGN 32
CharacterBasedEvents:
	CharacterEvent(MauritchDeathFlag,DaveRecruitment,Curtis,Dave)
	CharacterEvent(8,DaveRecruitmentLauren,Lauren,Dave)
	CharacterEventBothWays(9,DaveMauritchEvent,Mauritch,Dave)
	CharacterEventBothWays(10,DaveAltRecruitment,Curtis,Dave)
	END_MAIN

MiscBasedEvents:
	CauseGameOverIfLordDies
	AREA 0 BridgeCheck [12,8] [17,13]
	DefeatBoss(EndingScene)
	END_MAIN

LocationBasedEvents:

	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
	UNIT Curtis Eye Curtis Level(1,Ally,False) [15,18] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [14,17] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [16,17] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [13,18] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [17,18] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [14,19] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [16,19] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [15,16] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT

EnemyUnits:
	UNIT Caitlind_boss AxePeg Caitlind_boss Level(7,Enemy,False) [6,1] 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
	//UNIT Caitlind_boss AxePeg Caitlind_boss Level(7,Enemy,False) [11,18] 0x0 0x0 0x0 [IronAxe] AttackInRangeAI //Debug
	UNIT Dave Myrmidon Caitlind_boss Level(6,Enemy,False) [3,9] 0x0 0x0 0x0 [SteelSword, Vulnerary] AttackInRangeAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [5,2] 0x0 0x0 0x0 [IronLance] AttackInRangeAI
	UNIT Brigand_Enemy Outlaw_F Caitlind_boss Level(6,Enemy,True) [2,8] DropItem 0x0 0x0 0x0 [ShiningBow] AttackInRangeAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [10,15] 0x0 0x0 0x0 [IronLance] AttackInRangeAI
	UNIT Brigand_Enemy Mercenary_F Caitlind_boss Level(6,Enemy,True) [1,17] 0x0 0x0 0x0 0x0 [IronSword] TwoActionAI
	UNIT Brigand_Enemy Fighter_F Caitlind_boss Level(6,Enemy,True) [2,18] 0x0 0x0 0x0 0x0 [IronAxe] TwoActionAI
	UNIT Brigand_Enemy Mercenary_F Caitlind_boss Level(6,Enemy,True) [14,9] 0x0 0x0 0x0 0x0 [ArmorSlayer] AttackInRangeAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [5,13] 0x0 0x0 0x0 0x0 [IronLance] AttackInRangeAI
	UNIT Brigand_Enemy AxePeg Caitlind_boss Level(6,Enemy,True) [6,14] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
	UNIT
	
TrapPegas:
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [12,1] 0x0 0x0 0x1 REDA12R2 [IronLance] DefaultAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [13,2] 0x0 0x0 0x1 REDA13R1 [IronLance] DelayedChargeAI
	UNIT Brigand_Enemy AxePeg Caitlind_boss Level(6,Enemy,True) [14,1] 0x0 0x0 0x1 REDA14R2 [IronAxe] DefaultAI
	UNIT
	
CaitlindReal:
	UNIT Caitlind AxePeg Curtis Level(7,Ally,False) [0,0] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT
	
CutsceneUnitsBlue:
	UNIT Vidar Eye Curtis Level(1,Ally,False) [14,19] 0x00 0x00 0x01 REDA14R18 [0x0] NoAI
	UNIT Bobbe AxeArmor Curtis Level(1,Ally,False) [15,19] 0x0 0x0 0x1 REDA15R19 [0x0] NoAI
	UNIT Nishay BowArmor Curtis Level(1,Ally,False) [13,19] 0x0 0x0 0x1 REDA13R19 [0x0] NoAI
	UNIT 
	
CutsceneUnitsRed:
	UNIT Caitlind AxePeg Caitlind_boss Level(7,Enemy,False) [15,9] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT Ellie PegasusKnight Caitlind_boss Level(7,Enemy,False) [14,8] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT Ferryn AxePeg Caitlind_boss Level(7,Enemy,False) [16,8] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT
	
Traps1:
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:

	
	// Dave Recruitment things
	ENUT 10
	ENUT DaveFlag
	//ENUT MauritchDeathFlag // Debug
	
	LOAD1 0x2 CutsceneUnitsBlue
	ENUN
	LOAD1 0x2 CutsceneUnitsRed
	ENUN

	TEXTSTART
	TEXTSHOW Ch5OpeningText
	TEXTEND
	
	MOVE 0 Caitlind [15,15]
	MOVE 0 Ellie [14,14]
	MOVE 0 Ferryn [16,14]
	ENUN
	
	TEXTCONT
	TEXTEND
	REMA
	
	//Alysa thing
	CHECK_ALIVE Alysa
	BEQ 7 sC s0
	
	LABEL 7
	FADI 6
	STAL 6
	REMOVEALL_RED
	LOAD1 0x1 EnemyUnits
	ENUN
	LOAD1 0x0 Units1
	ENUN	
	GotoPrepScreen
	ENDA

DaveRecruitment:
	
	MUSS DrunkAsILikeID
	TEXTSTART
	TEXTSHOW DaveLordA
	TEXTEND
	ENUF DaveFlag	
	RECLASS Dave Mercenary
	//MUSC DanceBraceID
	ENUN
	TEXTCONT
	TEXTEND
	REMA
	CUSA Dave
	ENUN
	ENUT 8
	MURE 3
	NoFade
	ENDA
	
DaveRecruitmentLauren:
	
	CHECK_EVENTID MauritchDeathFlag
	BEQ 3 sC s0
	// Mauritch dead
	MUSS SadStoryID
	TEXTSTART
	TEXTSHOW DaveLaurenA
	TEXTEND
	SOUN SlapSFX
	STAL 30
	RECLASS Dave Mercenary
	ENUN
	ENUF DaveFlag
	TEXTCONT
	TEXTEND
	REMA
	GOTO 4
	
	// Mauritch alive
	LABEL 3
	MUSS DrunkAsILikeID
	TEXTSTART
	TEXTSHOW DaveLaurenB
	TEXTEND
	RECLASS Dave Mercenary
	ENUN
	ENUF DaveFlag
	TEXTCONT
	TEXTEND
	REMA
	
	LABEL 4
	CUSA Dave
	ENUN
	ENUT MauritchDeathFlag
	NoFade
	ENDA

DaveMauritchEvent:

	MUSS DrunkAsILikeID
	CHECK_ALLEGIANCE Dave
	BEQ 1 sC s0
	ENUT 8
	ENUT MauritchDeathFlag
	ENUF 10
	TEXTSTART
	TEXTSHOW DaveMauritchA
	TEXTEND
	RECLASS Dave Mercenary
	ENUN
	ENUF DaveFlag
	TEXTCONT
	TEXTEND
	REMA
	CUSN Dave
	ENUN
	SETVAL 1 0x00010A06
	CHAI Dave
	GOTO 2
	
	LABEL 1
	Text(DaveMauritchB)
	
	LABEL 2
	MURE 3
	NoFade
	ENDA

DaveAltRecruitment:

	MUSS DanceBraceID
	Text(DaveLordB)
	CUSA Dave
	MURE 3	
	NoFade
	ENDA
	
BridgeCheck:

	ContinueIfFactionMatch(0x0)
	COUNTER_INC 0
	COUNTER_CHECK 0
	SVAL 1 3
	BNE 5 s1 sC
	
	MUSCFAST WindDestinationID
	STAL 30
	EARTHQUAKE 1 1
	TILECHANGE 0
	SOUN CrashSFX
	STAL 60
	EARTHQUAKE_END
	ENUT 4
	STAL 30
	
	CAMERA [10,2]
	Text(PegaAmbushText)
	LOAD1 0x0 TrapPegas
	ENUN
	
	LABEL 5
	NoFade
	ENDA

Turn2Escapee:

	CAMERA [2, 8]
	CURSOR_FLASHING [2, 8]
	STAL 60
	REMOVE_CURSORS	
	Text(Ch5OutlawEscapeText)
	SVAL 1 0x00041000
	CHAI [2,8]

	NoFade
	ENDA

Turn3Escapee:
	SVAL 1 0x00040C00
	CHAI [7,8]
	NoFade
	ENDA
	
EndingScene:

	CHECK_EVENTID 11
	BEQ 6 s0 sC
	CHECK_ALIVE Alysa
	BEQ 6 s0 sC
	
	//Caitlind recruit
	Text(CaitlindRecruitText)
	LOAD1 0x1 CaitlindReal
	
	//Generic end events
	LABEL 6
	Text(Ch5EndingText)
	
	_0xA640 0 0 4
	WM_SETDESTINATION 0 5 0
	MNCH Chapter6
	NoFade
	ENDA