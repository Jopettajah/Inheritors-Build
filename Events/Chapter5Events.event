EventPointerTable(Chapter5Events, PointerList)

setText(0x164, NewChName)

ALIGN 4
NewChName:
String("Ch5: Burdened Wings")
BYTE 0

ALIGN 4
PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
	TurnEventPlayer(0,DaveOpening,1)
	TurnEventPlayer(0,Turn2Escapee,2)
	TurnEventPlayer(0,Turn3Escapee,3)
	TurnEventPlayer(14,PegAIChange,0,255)
	END_MAIN

ALIGN 32
CharacterBasedEvents:
	CharacterEvent(MauritchDeathFlag,DaveRecruitment,Curtis,Dave)
	CharacterEvent(8,DaveRecruitmentLauren,Lauren,Dave)
	CharacterEventBothWays(9,DaveMauritchEvent,Mauritch,Dave)
	CharacterEventBothWays(10,DaveAltRecruitment,Curtis,Dave)
	END_MAIN

MiscBasedEvents:
	CauseGameOverIfLordDies
	AREA 0 BridgeCheck [12,8] [17,13]
	DefeatBoss(EndingScene)
	END_MAIN

LocationBasedEvents:

	END_MAIN

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
	UNIT Curtis Eye Curtis Level(1,Ally,False) [14,18] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [13,17] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [15,17] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [12,18] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [16,18] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [13,19] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [15,19] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [14,16] 0x00 0x00 0x00 0x01 [0x0] NoAI
	//UNIT Jacob SwordCavalier Curtis Level(1,Ally,False) [3,10] 0x00 0x00 0x00 0x01 [0x0] NoAI
	UNIT

EnemyUnits:
	UNIT Caitlind_boss AxePeg Caitlind_boss Level(7,Enemy,False) [5,1] 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
	//UNIT Caitlind_boss AxePeg Caitlind_boss Level(7,Enemy,False) [11,18] 0x0 0x0 0x0 [IronAxe] AttackInRangeAI //Debug
	UNIT Dave Myrmidon Caitlind_boss Level(7,Enemy,False) [5,3] 0x0 0x0 0x0 [SteelSword, Vulnerary] AttackInRangeAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [4,2] 0x0 0x0 0x0 [IronLance] MoveWithLeaderAI
	UNIT Brigand_Enemy Outlaw_F Caitlind_boss Level(6,Enemy,True) [6,2] 0x0 0x0 0x0 0x0 [IronBow] MoveWithLeaderAI
	UNIT Brigand_Enemy Outlaw_F Caitlind_boss Level(6,Enemy,True) [2,8] DropItem 0x0 0x0 0x0 [ShiningBow] AttackInRangeAI
	//UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [10,15] 0x0 0x0 0x0 [IronLance] AttackInRangeAI
	UNIT Brigand_Enemy Mercenary_F Caitlind_boss Level(6,Enemy,True) [1,17] 0x0 0x0 0x0 0x0 [IronSword] TwoActionAI
	UNIT Brigand_Enemy Fighter_F Caitlind_boss Level(6,Enemy,True) [2,18] 0x0 0x0 0x0 0x0 [IronAxe] TwoActionAI
	UNIT Brigand_Enemy Mercenary_F Caitlind_boss Level(6,Enemy,True) [14,9] 0x0 0x0 0x0 0x0 [ArmorSlayer] AttackInRangeAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [5,13] 0x0 0x0 0x0 0x0 [IronLance]  DelayedChargeAI //AttackInRangeAI
	UNIT Brigand_Enemy AxePeg Caitlind_boss Level(6,Enemy,True) [6,14] 0x0 0x0 0x0 0x0 [IronAxe] DefaultAI //AttackInRangeAI
	UNIT Brigand_Enemy AxePeg Caitlind_boss Level(6,Enemy,True) [19,7] 0x0 0x0 0x0 0x0 [IronAxe] DelayedChargeAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [21,15] 0x0 0x0 0x0 0x0 [IronLance] DefaultAI
	UNIT Brigand_Enemy Fighter_F Caitlind_boss Level(6,Enemy,True) [15,3] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI
	UNIT Brigand_Enemy Myrmidon_F Caitlind_boss Level(6,Enemy,True) [3,4] 0x0 0x0 0x0 0x0 [SteelSword] TwoActionAI //MoveWithLeaderAI
	UNIT Brigand_Enemy Monk_F Caitlind_boss Level(6,Enemy,True) [1,4] 0x0 0x0 0x0 0x0 [Shinespark] TwoActionAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [1,7] 0x0 0x0 0x0 0x0 [IronLance] AttackInRangeAI
	UNIT
	
TrapPegas:
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [11,1] 0x0 0x0 0x1 REDA11R2 [IronLance] DelayedChargeAI
	UNIT Brigand_Enemy PegasusKnight Caitlind_boss Level(6,Enemy,True) [12,2] 0x0 0x0 0x1 REDA12R1 [IronLance] AttackInRangeAI
	UNIT Brigand_Enemy AxePeg Caitlind_boss Level(6,Enemy,True) [13,1] 0x0 0x0 0x1 REDA13R2 [IronAxe] DelayedChargeAI
	UNIT
	
CaitlindReal:
	UNIT Caitlind AxePeg Curtis Level(7,Ally,False) [0,0] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT
	
CutsceneUnitsBlue:
	UNIT Vidar Eye Curtis Level(1,Ally,False) [14,19] 0x00 0x00 0x01 REDA14R18 [0x0] NoAI
	UNIT Bobbe AxeArmor Curtis Level(1,Ally,False) [15,19] 0x0 0x0 0x1 REDA15R19 [0x0] NoAI
	UNIT Nishay BowArmor Curtis Level(1,Ally,False) [13,19] 0x0 0x0 0x1 REDA13R19 [0x0] NoAI
	UNIT 
	
CutsceneUnitsRed:
	UNIT Caitlind AxePeg Caitlind_boss Level(7,Enemy,False) [14,9] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT Ellie PegasusKnight Caitlind_boss Level(7,Enemy,False) [13,8] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT Ferryn AxePeg Caitlind_boss Level(7,Enemy,False) [15,8] 0x0 0x0 0x0 0x1 [IronAxe] NoAI
	UNIT
	
CutsceneAlysa:
	UNIT Henrik Recruit_1 Curtis Level(1,Ally,False) [14,19] 0x00 0x00 0x01 REDA14R17 [0x0] NoAI
	UNIT
	
Traps1:
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:

	MUSCFAST WindSFX
	
	// Dave Recruitment things
	ENUT 10
	ENUT DaveFlag
	//ENUT MauritchDeathFlag // Debug
	
	//Third ambush peg delay flag
	ENUT 14
	
	LOAD1 0x2 CutsceneUnitsBlue
	ENUN
	LOAD1 0x2 CutsceneUnitsRed
	ENUN

	STAL 50
	
	TEXTSTART
	TEXTSHOW Ch5OpeningText
	TEXTEND
	
	MUSC TroubleID
	TEXTCONT
	TEXTEND
	
	MOVE 0 Caitlind [14,15]
	MOVE 0 Ellie [13,14]
	MOVE 0 Ferryn [15,14]
	ENUN
	
	STAL 30
	
	TEXTCONT
	TEXTEND
	REMA
	
	STAL 50
	

	MOVE 0 Ellie [13,8]
	MOVE 0 Ferryn [15,8]
	MOVE 0 Caitlind [14,11]
	ENUN
	
	//Alysa thing
	CHECK_ALIVE Alysa
	BEQ 7 sC s0
	TEXTSTART
	TEXTSHOW AlysaIntroText
	TEXTEND
	
	LOAD1 0x2 CutsceneAlysa
	ENUN
	
	TEXTCONT
	TEXTEND
	MOVE 0 Caitlind [14,9]
	ENUN
	TEXTCONT
	TEXTEND
	REMA
	
	LABEL 7
	MOVE 0 Caitlind [14,9]
	ENUN
	
	FADI 6
	STAL 6
	REMOVEALL_RED
	LOAD1 0x1 EnemyUnits
	ENUN
	LOAD1 0x0 Units1
	ENUN	
	GotoPrepScreen
	ENDA

DaveOpening:
	
	MUSS DrunkAsILikeID
	CAMERA Caitlind_boss
	CURSOR_FLASHING Caitlind_boss
	STAL 40
	REMOVE_CURSORS
	CURSOR_FLASHING Dave
	STAL 40
	REMOVE_CURSORS
	
	TEXTSTART
	TEXTSHOW Ch5DaveIntro
	TEXTEND

	MOVE 0 Dave [3,9]
	//MOVE 0 Dave [11,18]
	ENUN
	CAMERA Dave	
	CURSOR_FLASHING Dave
	STAL 40
	REMOVE_CURSORS
	CURSOR_FLASHING [2,8]
	STAL 40
	REMOVE_CURSORS

	TEXTCONT
	TEXTEND
	REMA
	
	MUSCFAST LakesNMarshesID
	CAMERA Curtis
	CURSOR_FLASHING [14,14]
	STAL 50
	REMOVE_CURSORS
	Text(Ch5BridgeText)

	NoFade
	ENDA
	
DaveRecruitment:
	
	MUSS DrunkAsILikeID
	TEXTSTART
	TEXTSHOW DaveLordA
	TEXTEND
	ENUF DaveFlag	
	RECLASS Dave Mercenary
	//MUSC DanceBraceID
	ENUN
	TEXTCONT
	TEXTEND
	REMA
	CUSA Dave
	ENUN
	ENUT 8
	ENUT 15
	MURE 3
	NoFade
	ENDA
	
DaveRecruitmentLauren:
	
	CHECK_EVENTID MauritchDeathFlag
	BEQ 3 sC s0
	// Mauritch dead
	MUSS SadStoryID
	TEXTSTART
	TEXTSHOW DaveLaurenA
	TEXTEND
	SOUN SlapSFX
	STAL 30
	RECLASS Dave Mercenary
	ENUN
	ENUF DaveFlag
	TEXTCONT
	TEXTEND
	REMA
	GOTO 4
	
	// Mauritch alive
	LABEL 3
	MUSS DrunkAsILikeID
	TEXTSTART
	TEXTSHOW DaveLaurenB
	TEXTEND
	RECLASS Dave Mercenary
	ENUN
	ENUF DaveFlag
	TEXTCONT
	TEXTEND
	REMA
	
	LABEL 4
	CUSA Dave
	ENUN
	ENUT MauritchDeathFlag
	ENUT 15
	MURE 3
	NoFade
	ENDA

DaveMauritchEvent:

	MUSS DrunkAsILikeID
	CHECK_ALLEGIANCE Dave
	BEQ 1 sC s0
	ENUT 8
	ENUT MauritchDeathFlag
	ENUF 10
	TEXTSTART
	TEXTSHOW DaveMauritchA
	TEXTEND
	RECLASS Dave Mercenary
	ENUN
	ENUF DaveFlag
	TEXTCONT
	TEXTEND
	REMA
	CUSN Dave
	ENUN
	SETVAL 1 0x00010A06
	CHAI Dave
	GOTO 2
	
	LABEL 1
	Text(DaveMauritchB)
	
	LABEL 2
	ENUT 15
	MURE 3
	NoFade
	ENDA

DaveAltRecruitment:

	MUSS DanceBraceID
	Text(DaveLordB)
	CUSA Dave
	MURE 3
	ENUT 15
	NoFade
	ENDA
	
BridgeCheck:

	ContinueIfFactionMatch(0x0)
	COUNTER_INC 0
	COUNTER_CHECK 0
	SVAL 1 3
	BNE 5 s1 sC
	
	MUSCFAST WindDestinationID
	STAL 30
	EARTHQUAKE 1 1
	TILECHANGE 0
	SOUN CrashSFX
	STAL 60
	EARTHQUAKE_END
	ENUT 4
	STAL 30
	
	CAMERA [8,2]
	Text(PegaAmbushText)
	LOAD1 0x0 TrapPegas
	ENUN
	ENUF 14
	
	LABEL 5
	NoFade
	ENDA

PegAIChange:
	SVAL 1 0x00041200
	CHAI [13,2]
	NoFade
	ENDA
	
Turn2Escapee:

	CAMERA [2, 8]
	CURSOR_FLASHING [2, 8]
	STAL 60
	REMOVE_CURSORS	
	Text(Ch5OutlawEscapeText)
	SVAL 1 0x00041000
	CHAI [2,8]

	NoFade
	ENDA

Turn3Escapee:
	SVAL 1 0x00040C00
	CHAI [7,8]
	NoFade
	ENDA
	
EndingScene:

	SetBackground(Fort_Ruins_Outside)
	CHECK_EVENTID 11
	BEQ 6 s0 sC
	CHECK_ALIVE Alysa
	BEQ 6 s0 sC
	
	//Caitlind recruit
	MUSCFAST KnightsOathID
	Text(CaitlindRecruitText)
	LOAD1 0x1 CaitlindReal
	
	//Generic end events
	LABEL 6
	MUSCFAST Victory
	Text(Ch5EndingText)
	//EndConvo
	FADI 16
	
	_0xA640 0 0 4
	WM_SETDESTINATION 0 5 0
	MNCH Chapter6
	NoFade
	ENDA